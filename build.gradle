/*
 * NOTES
 * 
 * ORM: 
 *    - Kundera: JPA compliant, easily switch between NoSQL and relational databases, uses SQL-like query langauge (ugh)
 *    - Morphia: mongodb-specific, not actively developed
 *    - Spring Data: heavy, provides decent separation
 *    
 * SECURITY:
 *    - Spring: role-based, XML
 *    - Apache Shiro: INI files
 * 
 */

import org.slf4j.Logger
import org.slf4j.LoggerFactory

apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'war'
apply plugin: 'jetty'

apply from: './config-eclipse.gradle'
apply from: './check-mongo.gradle'

// PROJECT VERSION & GROUP
project.version = 0.1
project.group = 'com.moksamedia.moksalizer'

// CAPTURE STANDARD OUTPUT
logging.captureStandardOutput(LogLevel.ERROR)

import org.slf4j.Logger
import org.slf4j.LoggerFactory


Logger log = LoggerFactory.getLogger("build-logger")

buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'eu.appsatori:gradle-fatjar-plugin:0.1.2' // adds fatJar task
	}
}

repositories {
	mavenCentral()
	maven {
		url "http://bits.netbeans.org/maven2"
	}
}
	
dependencies {
		
	/* JUNIT */
	testCompile 'junit:junit:4.10'
		
	/* LOGGING */
	compile 'org.slf4j:slf4j-api:1.7.4',
			'ch.qos.logback:logback-classic:1.0.1'  // Logback
			//'org.slf4j:slf4j-log4j12:1.7.4',	    // Log4j
			//'log4j:log4j:1.2.17'		
	
	/* GROOVY */
	groovy 'org.codehaus.groovy:groovy-all:1.8.+'
	
	/* SERVLET API 2.5 */
	compile 'javax.servlet:servlet-api:2.5'
	
	/* JAX-RS (JSR 311) API */
	providedCompile 'javax.ws.rs:jsr311-api:1.1.1'
	
	/* JERSEY */
	compile 'com.sun.jersey:jersey-bundle:1.17'
	
	/* JERSEY GUICE - DEPENDENCY INJECTION */
	compile 'com.sun.jersey.contribs:jersey-guice:1.17'
	
	/* MONGO JAVA DRIVER */
	compile 'org.mongodb:mongo-java-driver:2.10.1'
	
	/* MORPHIA (https://github.com/jmkgreen/morphia) */
	compile 'com.github.jmkgreen.morphia:morphia:1.2.2'
	compile 'com.github.jmkgreen.morphia:morphia-logging-slf4j:1.2.2'
	
	/* GOOGLE DRIVE API */
	compile 'com.google.api-client:google-api-client:1.14.1-beta'
	
	/* HTTP BUILDER */
	compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.5.2'
	
	/* HTML PARSER */
	compile 'org.htmlparser:htmlparser:1.6'
	
	/* APACHE SHIRO - SECURITY */
	compile 'org.apache.shiro:shiro-all:1.2.1'
	
	compile project(':plugin-api')
	
	//runtime project(':plugin-test')
		
}

ext.keystore = file('src/main/resources/jetty-ssl.keystore').getAbsolutePath()

[jettyRun, jettyRunWar]*.configure {
	contextPath = ''
	jettyConfig= file('jetty-web.xml')
}

war {
	webXml = file('web.xml')
	//classpath configurations.compile
}

/*
 * Automatically triggers subprojects' eclipse task when
 * eclipse task is run on root project
 */
tasks.eclipse.dependsOn{
	subprojects { Project subproj ->
		subproj.tasks.eclipse
	}
}

/*
 * Automatically adds a runtime dependency for root project for
 * all subprojects that have the ext.plugin property defined. Thus
 * can comment this out to not include the plugin.
 */
subprojects {
	afterEvaluate { Project subproj ->
		if (subproj.hasProperty('plugin')) {
			rootProject.dependencies { runtime subproj }
		}
	}
}

/*
 * Only run tests, jettyRun, and jettyRunWar if mongo db is running.
 */
test.onlyIf { mongoOK }
tasks.jettyRun.dependsOn requireMongo
tasks.jettyRunWar.dependsOn requireMongo



/*
 * Copies plugin jars from plugin subproject lib directories to
 * plugin-lib subdirectory of root project.
 *
 * May not be needed.
 */
/*
build << {
	delete "plugin-lib/*.jar"
	subprojects { subproj ->
		if (subproj.hasProperty('plugin')) {
			copy {
				println "Copying plugin jar from ${subproj.libsDir}"
				from(subproj.libsDir) {
					include subproj.jar.archiveName
				}
				into "${rootDir}/plugin-lib"
			}
		}
	}
}
*/

/*
jar {
	from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
}
*/

