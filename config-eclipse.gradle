/*
 * A hack to exclude the groovy script and config files from the eclipse classpath
 * so that eclipse doesn't think it's an error that we have a config.groovy in 
 * both the src/main/resources and src/test/resources folders.
 */
eclipse.classpath.file.withXml {
	def node = it.asNode()
	def children = node.children()
	children.each {
		if (it.@path == 'src/main/resources' || it.@path == 'src/test/resources') {
			it.@excluding = '*.groovy'
			println "Excluding *.groovy files from ${it.@path}"
		}
	}
}

task 'remove-classpath-file' << {
	String path = rootDir.path + '/.classpath'
	println "Removing classpath file: $path"
	delete file(path)
}

/*
 * Have to delete the classpath file each time because if we don't
 * we end up with double entries for the src/main/resources and
 * src/test/resources files.
 */
tasks.eclipseClasspath.dependsOn 'remove-classpath-file'

/*
 *	Adds src/main/webapp to the build path.
 */
eclipse.classpath.file.withXml {
	def node = it.asNode()
	node.appendNode('classpathentry', [kind: 'src', path: 'src/main/webapp'])
}
